COMMAND MKDIR;
!=======================================================================
! Make a subdirectory in the current directory.
! Usage:
!	MKDIR subdirname
!
! Examples (assume connected directory is FOO:<BAR>):
!	MKDIR mung	! makes FOO:<BAR.MUNG>
!	MKDIR mung.ho	! makes FOO:<BAR.MUNG.HO>
!
! [02-Jul-87]
!=======================================================================
{
    STRING 
	thedir,
	subdir;

    PARSE (TEXT : subdir = $Atom);
    PARSE (EOL : NOP);

    thedir = $ConnectedDirectory;
    thedir = thedir[1:$Length(thedir)-1] + "." + subdir + ">";
    DOCOMMAND ORIGINAL "ENABLE";
    DOCOMMAND ORIGINAL "BUILD " + thedir + $CRLF +
	"FILES"  + $CRLF +
	"MAX 0" + $CRLF +
	"PRESERVE" + $CRLF +
	"WORK 1000000" + $CRLF +
	"PERM 1000000" + $CRLF +
	! Need the next two for Monitor 6.1--new defaults are 777700
	"default-file-protection 777752" + $CRLF +
	"protection 777742" + $CRLF +
	$CRLF + $CRLF;
    DOCOMMAND ORIGINAL "SET FILE PROTECTION " + $ConnectedDirectory +
        subdir + ".DIRECTORY 424242";
    DOCOMMAND ORIGINAL "DISABLE";
}
