COMMAND MAKE_INVISIBLE;
!------------------------------------------------------------------------
! Purpose:
!	Set OFFLINE files invisible.
!
! Usage:
!	MAKE-INVISIBLE filespecs
!
! Remarks:
!	The filespecs field may contain any list of file
!	specifications which would be acceptable to the DIRECTORY
!	command.  This command remedies the deficiency of the 
!	SET INVISIBLE command, which does not permit wild-card
!	specifications.
!
! Author:
!	Nelson H.F. Beebe, Department of Physics, University of Utah,
!	Salt Lake City, UT 84112.
!
! Date:
!	[18-Jul-83]
!
!------------------------------------------------------------------------
BEGIN

GUIDE "OFFLINE files only";

PARSE (FILELIST 
	(DEFAULT_DEV "DSK",
	DEFAULT_NAM "*",
	DEFAULT_EXT "*",
	DEFAULT_GEN *,
	HELP "list of files (default DSK:*.*.*)",
	WILD) : NOP);

DO 
        IF $AND($FileInfo_I($Parse,$FBCTL),8%000020000000) <> 0 
	THEN 
		{
		DOCOMMAND ORIGINAL "SET FILE INVISIBLE " + $FileL
		}
UNTIL $Nextfile = 0
END; !Make_Invisible
